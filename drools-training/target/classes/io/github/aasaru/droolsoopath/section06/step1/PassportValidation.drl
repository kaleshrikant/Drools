package io.github.aasaru.droolsoopath.section06.step1

unit RuleUnitSection06;

import io.github.aasaru.drools.domain.Passport;
import io.github.aasaru.drools.domain.Validation;

rule "Valid passport"
  dialect "java"
  when
    $passport: /passports[ validation == Validation.UNKNOWN ]
  then
    System.out.println( $passport + " is without validation info, consider OK for now" );
    modify($passport) {
      setValidation( Validation.PASSED )
    }
end

rule "Expired passport"
  dialect "java"
  when
    $passport: /passports[ isExpired() ]
  then
    System.out.println( $passport + " is INVALID because it is expired" );
    modify($passport) {
      setValidation( Validation.FAILED ),
      setCause( "passport is expired" )
    }
end

rule "Full passport"
  dialect "java"
  when
    $passport: /passports[ unusedVisaPages == 0 ]
  then
    System.out.println( $passport + " is INVALID because it has no space for visa" );
    modify($passport) {
      setValidation( Validation.FAILED ),
      setCause( "passport has no space for visa" )
    }
end
